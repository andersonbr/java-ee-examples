buildscript {
  ext {
    javaVersion = "1.6"
    javaeeVersion = "6.0"
    slf4jVersion = "1.7.25"
    kotlinVersion = "1.2.30"
    lombokVersion = "1.16.20"
    resteasyVersion = "3.5.0.Final"
  }
}

plugins {
  //id "ear"
  id "war"
  id "idea"
  id "maven"
  id "eclipse"
  id "maven-publish"
  id "org.jetbrains.kotlin.jvm" version "1.2.30"
  id "org.jetbrains.kotlin.plugin.noarg" version "1.2.30"
  id "org.jetbrains.kotlin.plugin.allopen" version "1.2.30"
  id "com.avast.gradle.docker-compose" version "0.6.13"
}

group = "daggerok"
version = "0.0.1"
sourceCompatibility = targetCompatibility = "$javaVersion"

compileKotlin {
  incremental = true
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "$javaVersion"
  }
}

compileTestKotlin {
  incremental = true
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "$javaVersion"
  }
}

allOpen {
  annotation("javax.ws.rs.Path")
  annotation("javax.ejb.Startup")
  annotation("javax.ejb.Stateless")
  annotation("javax.ejb.Singleton")
  annotation("javax.inject.Singleton")
  annotation("javax.persistence.Entity")
  annotation("javax.enterprise.context.ApplicationScoped")
}

noArg {
  annotation("javax.ws.rs.Path")
  annotation("javax.ejb.Startup")
  annotation("javax.ejb.Stateless")
  annotation("javax.ejb.Singleton")
  annotation("javax.inject.Singleton")
  annotation("javax.persistence.Entity")
  annotation("javax.enterprise.context.ApplicationScoped")
}

/*
ear {
  archiveName = "${project.name}.ear"
}
*/

war {
  archiveName = "${project.name}.war"
}

defaultTasks "clean", "build"

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  //compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1")
  compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
  compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")

  providedCompile("javax:javaee-api:$javaeeVersion")
  providedCompile("org.jboss.resteasy:resteasy-jaxrs:$resteasyVersion")
  providedCompile("org.jboss.resteasy:resteasy-multipart-provider:$resteasyVersion")
  providedCompile("org.jboss.resteasy:resteasy-json-p-provider:$resteasyVersion")
  compile("javax.json:javax.json-api:1.0")
  compile("org.glassfish:javax.json:1.0.4")

  providedCompile("com.h2database:h2:1.4.196")
  compile("org.hibernate:hibernate-core:4.2.18.Final")

  compileOnly("org.projectlombok:lombok:$lombokVersion")
  compile("org.slf4j:slf4j-api:$slf4jVersion")
  compile("org.slf4j:slf4j-jdk14:$slf4jVersion")

  // junit 4
  testCompile("junit:junit:4.12")
  testImplementation("org.hamcrest:hamcrest-core:1.3")
  testCompileOnly("org.projectlombok:lombok:$lombokVersion")
}

def profile = project.hasProperty("profile") ? project.getProperty("profile") : "gradle"

dockerCompose {
  useComposeFiles = [project.file("docker-compose-${profile}.yaml")]
  // captureContainersOutput = true
  captureContainersOutput = false
  stopContainers = true
  removeContainers = true
  removeImages = "Local"
  removeVolumes = true
  removeOrphans = true
  forceRecreate = true
  waitForTcpPorts = false
  projectName = project.name
}

composeUp.dependsOn assemble

task wrapper(type: Wrapper) {
  gradleVersion = "4.6"
  distributionType = "ALL"
}

idea {
  module {
    downloadSources = true
  }
}

eclipse {
  classpath {
    downloadSources = true
  }
}

publishing {
  repositories {
    maven {
      url "$buildDir/maven-publish"
    }
  }
}
